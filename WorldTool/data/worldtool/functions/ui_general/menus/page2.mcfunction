scoreboard players operation #ID_temp worldtool = @s wt_ID
execute as @e[type=minecraft:area_effect_cloud,tag=worldtool,tag=wt_check] if score @s wt_ID = #ID_temp worldtool at @s run function worldtool:ui_general/kill_wt_check
execute as @e[type=minecraft:area_effect_cloud,tag=worldtool,tag=pos1] if score @s wt_ID = #ID_temp worldtool run function worldtool:ui_general/pos1_removetags

tag @s remove placing_fill
function worldtool:ui_general/clear_chat

execute as @e[type=minecraft:area_effect_cloud,tag=worldtool,tag=pos1,tag=keep] if score @s wt_ID = #ID_temp worldtool run tellraw @p ["",{"text":"[Checkered...]  ","color":"gold","hoverEvent":{"action":"show_text","value":"Place on every other block"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/checkered/select"}},{"text":"[Greenery...]  ","color":"gray","hoverEvent":{"action":"show_text","value":"Greenery cannot be selected because Keep is on"}},{"text": "[Outline...]  ","color":"light_purple","hoverEvent": {"action": "show_text","value": "Place blocks in the corners of the selected area"},"clickEvent": {"action": "run_command","value": "/function worldtool:ui_general/menus/outline/select"}},{"text":"[Command block list]  ","color":"aqua","hoverEvent":{"action":"show_text","value":"List the commands of the first command block chain found"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/cmd_list"}}]
execute as @e[type=minecraft:area_effect_cloud,tag=worldtool,tag=pos1,tag=!keep] if score @s wt_ID = #ID_temp worldtool run tellraw @p ["",{"text":"[Checkered...]  ","color":"gold","hoverEvent":{"action":"show_text","value":"Place on every other block"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/checkered/select"}},{"text":"[Greenery...]  ","color":"green","hoverEvent":{"action":"show_text","value":"Randomly place flowers and grass from a selected biome"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/greenery/select"}},{"text": "[Outline...]  ","color":"light_purple","hoverEvent": {"action": "show_text","value": "Place blocks in the corners of the selected area"},"clickEvent": {"action": "run_command","value": "/function worldtool:ui_general/menus/outline/select"}},{"text":"[Command block list]  ","color":"aqua","hoverEvent":{"action":"show_text","value":"List the commands of the first command block chain found"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/cmd_list"}}]
execute if entity @s[tag=!fun_menu] run tellraw @s {"text":"[Small stuff ▶...]","color":"blue","hoverEvent":{"action":"show_text","value":"Some random not-that-useful functions"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/small_stuff/toggle"}}
execute if entity @s[tag=fun_menu] run tellraw @s [{"text":"[Small stuff ▼...]","color":"blue","hoverEvent":{"action":"show_text","value":"Some random not-that-useful functions"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/small_stuff/toggle"}},{"text":"\n[Command block chain]","hoverEvent":{"action":"show_text","value":"Fill the selected area with a chain of chain command blocks"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/menus/small_stuff/cmd_block_fill"}},{"text": "\n[Corner structure blocks]","hoverEvent": {"action": "show_text","value": "Place Corner mode structure blocks around the selected area"},"clickEvent": {"action": "run_command","value": "/function worldtool:struct_corner_place/load"}}]
execute if score $generalToolAddons worldtool matches 1.. run tellraw @s {"text": "[Addons]","color":"light_purple","hoverEvent": {"action": "show_text","value": "Show a list of processes added by addons"},"clickEvent": {"action": "run_command","value": "/function worldtool:ui_general/addon_processes"}}
tellraw @s ["",{"text": "\nPage: ","color": "dark_green"},{"text":"< ","color":"light_purple","bold": true,"hoverEvent":{"action":"show_text","value":"Show the previous page"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/check"}},"2     ",{"text":"[Close]","color":"white","hoverEvent":{"action":"show_text","value":"Close all menus and cancel"},"clickEvent":{"action":"run_command","value":"/function worldtool:ui_general/cancel"}}]
function worldtool:ui_general/player_removetags
function worldtool:ui_general/anti_feedback_chat_message/load
